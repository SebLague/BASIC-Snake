00 REM MY FIRST BASIC PROJECT!
01 REM A TERRIBLE SNAKE IMPLEMENTATION BY SEBASTIAN LAGUE
02 REM INSPIRED BY https://www.youtube.com/watch?v=7r83N3c2kPw
03 REM RUN ON: https://www.calormen.com/jsbasic/

100 REM ------ CONSTANTS ------
110 WIDTH = 40 : HEIGHT = 48
120 BGCOL = 4
130 COLSNAKE = 12
140 FOODCOL = 13
150 NUMFOOD = 4
160 DIM XSNAKE(WIDTH*HEIGHT) : DIM YSNAKE(WIDTH*HEIGHT)

200 REM ------ SET UP ------
210 L = 1 : REM SNAKE LENGTH
220 XDIR = -1 : YDIR = 0
300 XSNAKE(0) = WIDTH / 2
310 YSNAKE(0) = HEIGHT / 2
500 GR : POKE 49234,0 : REM FULLSCREEN LORES GRAPHICS MODE
510 COLOR = BGCOL : GOSUB 9000 : REM DRAW BACKGROUND
520 REM SPAWN SOME FOOD
525 RNGSTATE = RND(1) : T = RND(1) : REM DON'T KNOW HOW TO CHANGE RANDOM SEED
530 FOR FI = 0 TO NUMFOOD-1
540 GOSUB 4000
550 NEXT FI

1000 REM ------ GAME LOOP ------
1010 GOSUB 10000 : REM FRAMERATE CONTROL
1020 GOSUB 3000 : REM HANDLE INPUT

1099 REM UPDATE SNAKE
1100 COLOR = BGCOL
1110 PLOT XSNAKE(L-1), YSNAKE(L-1)
1120 GOSUB 2000 : REM MOVE SNAKE
1130 SCREENCOL = SCRN(XSNAKE(0), YSNAKE(0))
1140 COLOR = COLSNAKE
1150 PLOT XSNAKE(0), YSNAKE(0)

1200 REM CHECK IF SNAKE ATE FOOD / OWN TAIL
1210 IF SCREENCOL = FOODCOL THEN L = L + 1 : GOSUB 4000
1220 IF SCREENCOL = COLSNAKE THEN GOTO 5000
1500 GOTO 1000 : REM END OF GAME LOOP

2000 REM MOVE SNAKE
2009 REM CALCULATE NEW HEAD POS (WITH SCREEN WRAPAROUND)
2010 XHEAD = XSNAKE(0) + XDIR : YHEAD = YSNAKE(0) + YDIR
2020 IF XHEAD < 0 THEN XHEAD = WIDTH -1
2030 IF XHEAD >= WIDTH THEN XHEAD = 0
2040 IF YHEAD < 0 THEN YHEAD = HEIGHT -1
2050 IF YHEAD >= HEIGHT THEN YHEAD = 0
2059 REM UPDATE SNAKE
2060 FOR I = 0 TO L - 1
2070 XSNAKE(L - I) = XSNAKE(L - I - 1)
2080 YSNAKE(L - I) = YSNAKE(L - I - 1)
2090 NEXT I
2100 XSNAKE(0) = XHEAD : YSNAKE(0) = YHEAD

3000 REM HANDLE INPUT
3005 XOLD = XDIR : YOLD = YDIR
3010 KEY = PEEK(49152)
3020 IF KEY = 215 THEN YDIR = -1 : XDIR = 0 : REM W
3030 IF KEY = 211 THEN YDIR = 1 : XDIR = 0 : REM S
3040 IF KEY = 193 THEN XDIR = -1 : YDIR = 0 : REM A
3050 IF KEY = 196 THEN XDIR = 1 : YDIR = 0 : REM D
3060 IF XDIR = -XOLD THEN XDIR = XOLD
3070 IF YDIR = -YOLD THEN YDIR = YOLD
3100 RETURN

4000 REM ------ SPAWN FOOD ------
4005 FOR I = 0 TO 10000 : REM HACKY FIND SPOT NOT OCCUPIED BY SNAKE
4010 XFOOD = INT((RND(1) * WIDTH))
4020 YFOOD = INT((RND(1) * HEIGHT))
4030 IF SCRN(XFOOD, YFOOD) = BGCOL THEN GOTO 4100
4040 NEXT I
4100 COLOR = FOODCOL : PLOT XFOOD, YFOOD : REM DRAW FOOD
4500 RETURN

5000 REM ------ GAMEOVER ------
5009 REM SHOW SIMPLE ANIMATION WHILE PLAYER CONTEMPLATES DEFEAT
5010 COLOR = 9 : GOSUB 9000 : REM FILL SCREEN WITH GAMEOVER COLOUR
5015 FOR W = 0 TO 1000 : NEXT
5016 COLOR = 0 : GOSUB 9000
5020 TEXT : HOME
5030 HTAB (15) : VTAB (10) : PRINT "GAME OVER!"
5040 HTAB (12) : VTAB (12) : PRINT "YOUR SCORE IS:"
5050 HTAB (27) : VTAB (12) : PRINT L
5060 HTAB (10) : VTAB (14) : PRINT "PRESS R TO PLAY AGAIN"
5070 IF PEEK(49152) == 210 THEN GOTO 200
5100 GOTO 5070

9000 REM ------ DRAW BACKGROUND ------
9020 FOR X = 0 TO WIDTH - 1
9025 FOR Y = 0 TO HEIGHT - 1
9030 PLOT X,Y
9035 NEXT Y
9040 NEXT X
9500 RETURN

10000 REM ------ SLOW FRAMERATE DOWN WITH EMPTY LOOP (LOL) ------
10010 FOR I = 0 TO 300 : NEXT I
10500 RETURN
